defaultTasks 'clean', 'build', 'install'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.5.RELEASE")
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.github.ben-manes.versions'

description = 'Spring Boot with mutual TLS'
group = 'ch.frostnova'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
}

jar { duplicatesStrategy = 'exclude' }

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

ext {
    checkVersion = '1.0.0'
    junitVersion = '4.12'
    mockitoVersion = '2.27.0'
    springVersion = '2.1.5.RELEASE'

    libs = [
            check         : [
                    "ch.frostnova:check:${checkVersion}"
            ],
            testbase      : [
                    "junit:junit:${junitVersion}",
                    "org.mockito:mockito-core:${mockitoVersion}"
            ],
            springBoot    : [
                    "org.springframework.boot:spring-boot-starter-web:${springVersion}",
                    "org.springframework.boot:spring-boot-starter-security:${springVersion}",
                    "org.springframework.boot:spring-boot-starter-actuator:${springVersion}"
            ],
            springBootTest: [
                    "org.springframework.boot:spring-boot-starter-test:${springVersion}",
                    "org.springframework.boot:spring-boot-starter-jersey:${springVersion}",
                    "javax.xml.bind:jaxb-api:2.2.11"
            ]
    ]
}

dependencies {
    compile libs.check
    compile libs.springBoot
    testCompile libs.testbase
    testCompile libs.springBootTest
}

task start(dependsOn: ['assemble', 'bootRun']) {
    group = 'Start/Run'
    description = 'Start the boot app'
}